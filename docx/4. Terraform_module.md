á» bÃ i nÃ y chÃºng ta sáº½ lÃ m vÃ­ dá»¥ táº¡o má»™t Virtual Private Cloud (VPC) á»Ÿ trÃªn AWS, thÃ´ng qua Ä‘Ã³ chÃºng ta sáº½ tÃ¬m hiá»ƒu vá» cÃ¡ch viáº¿t code tá»«ng bÆ°á»›c vÃ  sau Ä‘Ã³ tá»« má»™t Ä‘á»‘ng code nhÆ° tháº¿ thÃ¬ ta tá»• chá»©c code láº¡i nhÆ° tháº¿ nÃ o, lÃ m sao Ä‘á»ƒ sá»­ dá»¥ng láº¡i code hiá»‡u quáº£ nháº¥t mÃ  trÃ¡nh bá»‹ láº­p code nhiá»u nháº¥t cÃ³ thá»ƒ, báº±ng cÃ¡ch sá»­ dá»¥ng module.

Sau khi gÃ´m code thÃ nh module xong ta sáº½ tÃ¬m hiá»ƒu cÃ¡ch lÃ m sao Ä‘á»ƒ sá»­ dá»¥ng nÃ³ láº¡i cho cÃ¡c project khÃ¡c nhau, vÃ  cÃ¡ch Ä‘á»ƒ Ä‘áº©y module cá»§a ta lÃªn máº¡ng Ä‘á»ƒ má»i ngÆ°á»i cÃ³ thá»ƒ sá»­ dá»¥ng chung Ä‘Æ°á»£c. Háº¡ táº§ng mÃ  ta sáº½ xÃ¢y dá»±ng á»Ÿ bÃ i nÃ y nhÆ° sau.

![](https://images.viblo.asia/b2ffda60-d1e6-4bd2-b4a8-815b7e8f636e.jpg)

Provisioning Virtual Private Cloud
----------------------------------

ChÃºng ta sáº½ code tá»« Ä‘áº§u, ta sáº½ Ä‘i qua tá»«ng resource mÃ  Terraform dÃ¹ng Ä‘á»ƒ táº¡o ra má»™t VPC, mÃ¬nh cÅ©ng sáº½ giáº£i thÃ­ch sÆ¡ vá» lÃ½ thuyáº¿t cá»§a tá»«ng cÃ¡i trÃªn AWS. Äáº§u tiÃªn ta táº¡o má»™t file tÃªn lÃ Â `main.tf`Â vá»›i Ä‘oáº¡n code sau vÃ  cháº¡y cÃ¢u lá»‡nh init.

```
provider "aws" {
  region = "us-west-2"
}

```

```
terraform init

```

Oke, giá» ta sáº½ nÃ³i qua tá»«ng resource cá»§a AWS.

### VPC

VPC hiá»ƒu Ä‘Æ¡n giáº£n lÃ  má»™t máº¡ng áº£o ná»™i bá»™, nÃ³ lÃ  má»™t container mÃ  chá»©a toÃ n bá»™ cÃ¡c service cá»§a ta bÃªn trong network cá»§a nÃ³.

![image.png](https://images.viblo.asia/bfd90f71-3156-406c-982c-7d95a8a7aca3.png)

Máº·c Ä‘á»‹nh thÃ¬ má»—i region cá»§a AWS sáº½ cÃ³ má»™t VPC máº·c Ä‘á»‹nh tÃªn lÃ  default. Äá»ƒ táº¡o má»™t tháº±ng má»›i, ta dÃ¹ng resourceÂ `aws_vpc`Â cá»§a Terraform.

```
provider "aws" {
  region = "us-west-2"
}

resource "aws_vpc" "vpc" {
  cidr_block = "10.0.0.0/16"
  enable_dns_hostnames = true

    tags = {
    "Name" = "custom"
  }
}

```

á» trÃªn ta sáº½ táº¡o má»™t VPC má»›i vá»›i cidr lÃ  10.0.0.0/16 vÃ  tÃªn lÃ  Custom. CIDR cá»§a VPC sáº½ cÃ³ cÃ¡c giÃ¡ trá»‹ náº±m trong khoáº£ng sau:

-   10.0.0.0/16 -> 10.0.0.0/28
-   172.16.0.0/16 -> 172.16.0.0/28
-   192.168.0.0/16 -> 192.168.0.0/28

### Subnet

Subnet sáº½ chia VPC cá»§a ta ra thÃ nh nhiá»u sub network nhá» hÆ¡n. Má»—i subnet sáº½ náº±m trong má»™t AZ. VÃ  cÃ¡c service cá»§a ta sáº½ Ä‘Æ°á»£c launch á»Ÿ trong subnet nÃ y.

![image.png](https://images.viblo.asia/32488ec2-2929-442c-b658-8a822d31ef37.png)

Ta dÃ¹ngÂ `aws_subnet`Â cá»§a Terraform Ä‘á»ƒ táº¡o subnet.

```
...
resource "aws_subnet" "private_subnet_2a" {
  vpc_id     = aws_vpc.vpc.id
  cidr_block = "10.0.1.0/24"
  availability_zone = "us-west-2a"

  tags = {
    "Name" = "private-subnet"
  }
}

resource "aws_subnet" "private_subnet_2b" {
  vpc_id     = aws_vpc.vpc.id
  cidr_block = "10.0.2.0/24"
  availability_zone = "us-west-2b"

  tags = {
    "Name" = "private-subnet"
  }
}

resource "aws_subnet" "private_subnet_2c" {
  vpc_id     = aws_vpc.vpc.id
  cidr_block = "10.0.3.0/24"
  availability_zone = "us-west-2c"

  tags = {
    "Name" = "private-subnet"
  }
}

```

á» Ä‘oáº¡n code trÃªn ta sáº½ táº¡o 3 subnet lÃ  10.0.1.0/24, 10.0.2.0/24, 10.0.3.0/24 náº±m trong cÃ¡c zone a,b, c. Náº¿u ta cáº§n nhiá»u subnet hÆ¡n thÃ¬ ta cÃ³ thá»ƒ copy ra thÃªm má»™t resource block khÃ¡c, nhÆ°ng nhÆ° váº­y sáº½ khiáº¿n code cá»§a ta khÃ¡ dÃ i, ta cÃ³ thá»ƒ rÃºt gá»n code láº¡i nhÆ° sau.

```
...
locals {
  private = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  zone   = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

resource "aws_subnet" "private_subnet" {
  count = length(local.private)

  vpc_id            = aws_vpc.vpc.id
  cidr_block        = local.private[count.index]
  availability_zone = local.zone[count.index % length(local.zone)]

  tags = {
    "Name" = "private-subnet"
  }
}

```

Ta sáº½ thÃªm 3 subnet ná»¯a lÃ  10.0.4.0/24, 10.0.5.0/24, 10.0.6.0/24 (vÃ¬ sao tÃªn gá»i cá»§a cÃ¡c subnet nÃ y lÃ  public hay lÃ  private mÃ¬nh sáº½ giáº£i thÃ­ch á»Ÿ dÆ°á»›i nhÃ©).

```
...
locals {
  private  = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  public = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]
  zone    = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

resource "aws_subnet" "private_subnet" {
  count = length(local.private)

  vpc_id            = aws_vpc.vpc.id
  cidr_block        = local.private[count.index]
  availability_zone = local.zone[count.index % length(local.zone)]

  tags = {
    "Name" = "private-subnet"
  }
}

resource "aws_subnet" "public_subnet" {
  count = length(local.public)

  vpc_id            = aws_vpc.vpc.id
  cidr_block        = local.public[count.index]
  availability_zone = local.zone[count.index % length(local.zone)]

  tags = {
    "Name" = "public-subnet"
  }
}

```

BÃ¢y giá» khi service cá»§a chÃºng ta Ä‘Æ°á»£c táº¡o bÃªn trong cÃ¡c subnet nÃ y, thÃ¬ tá»¥i nÃ³ cÃ³ thá»ƒ nÃ³i chuyá»‡n vá»›i nhau Ä‘Æ°á»£c, nhÆ°ng náº¿u cÃ¡c service á»Ÿ trÃªn muá»‘n nÃ³i chuyá»‡n Ä‘Æ°á»£c vá»›i cÃ¡c tháº±ng khÃ¡c á»Ÿ bÃªn ngoÃ i internet thÃ¬ sáº½ khÃ´ng Ä‘Æ°á»£c, hoáº·c á»Ÿ bÃªn ngoÃ i muá»‘n gá»i vÃ o cÃ¡c service á»Ÿ bÃªn trong subnet cá»§a ta cÅ©ng sáº½ khÃ´ng Ä‘Æ°á»£c. VÃ¬ ta chÆ°a cÃ³ tháº±ng nÃ o Ä‘Ã³ng vai trÃ² router Ä‘á»ƒ cÃ¡c service cá»§a ta cÃ³ thá»ƒ nÃ³i chuyá»‡n Ä‘Æ°á»£c vá»›i internet bÃªn ngoÃ i cáº£.

![](https://images.viblo.asia/119ba789-c698-482f-8047-d8095c10925a.jpg)

### Internet gateway

Äá»ƒ cÃ¡c service bÃªn trong subnet cá»§a ta cÃ³ thá»ƒ tÆ°Æ¡ng tÃ¡c Ä‘Æ°á»£c vá»›i bÃªn ngoÃ i, ta cáº§n má»™t tháº±ng tÃªn lÃ  internet gateway (IG), vÃ  ta sáº½ gÃ¡n tháº±ng IG nÃ y vÃ o tháº±ng route table. Sau Ä‘Ã³ ta gÃ¡n subnet nÃ o mÃ  ta muá»‘n nÃ³ cÃ³ thá»ƒ tÆ°Æ¡ng tÃ¡c Ä‘Æ°á»£c vá»›i internet bÃªn ngoÃ i vÃ o route table nÃ y.

![](https://images.viblo.asia/cd422d9c-e4ef-493e-bfc2-e000668a92eb.jpg)

Tá»« Ä‘Ã¢y ta má»›i cÃ³ khÃ¡i niá»‡m lÃ  public subnet vÃ  private subnet, public subnet lÃ  subnet mÃ  cÃ¡c service bÃªn trong nÃ³ cÃ³ thá»ƒ tÆ°Æ¡ng tÃ¡c vá»›i internet bÃªn ngoÃ i vÃ  ngÆ°á»£c láº¡i bÃªn ngoÃ i cÅ©ng cÃ³ thá»ƒ tÆ°Æ¡ng tÃ¡c vá»›i nÃ³ thÃ´ng qua IG, cÃ²n Ä‘á»‘i vá»›i private subnet thÃ¬ cÃ¡c service bÃªn trong nÃ³ cÃ³ thá»ƒ tÆ°Æ¡ng tÃ¡c Ä‘Æ°á»£c vá»›i bÃªn ngoÃ i,Â nhÆ°ng bÃªn ngoÃ i khÃ´ng thá»ƒ tÆ°Æ¡ng tÃ¡c ngÆ°á»£c láº¡i nÃ³ Ä‘Æ°á»£c. Ta dÃ¹ng resourceÂ `aws_internet_gateway`Â Ä‘á»ƒ táº¡o IG.

```
...
resource "aws_internet_gateway" "ig" {
  vpc_id = aws_vpc.vpc.id

  tags = {
    "Name" = "custom"
  }
}

```

GÃ¡n nÃ³ vÃ o route table.

```
...
resource "aws_internet_gateway" "ig" {
  vpc_id = aws_vpc.vpc.id

  tags = {
    "Name" = "custom"
  }
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.ig.id
  }

  tags = {
    "Name" = "public"
  }
}

```

GÃ¡n route table cho cÃ¡c subnet.

```
...
resource "aws_route_table_association" "public_association" {
  for_each       = { for k, v in aws_subnet.public_subnet : k => v }
  subnet_id      = each.value.id
  route_table_id = aws_route_table.public.id
}

```

![](https://images.viblo.asia/0fe2595a-acca-4e55-8cae-5c6aa8ed4027.jpg)

Oke, giá» cÃ¡c service cá»§a ta bÃªn trong public subnet cÃ³ thá»ƒ tÆ°Æ¡ng tÃ¡c Ä‘Æ°á»£c vá»›i bÃªn ngoÃ i, váº­y cÃ²n cÃ¡c private subnet thÃ¬ sao? Hiá»‡n táº¡i thÃ¬ nÃ³ khÃ´ng thá»ƒ tÆ°Æ¡ng tÃ¡c Ä‘Æ°á»£c vá»›i bÃªn ngoÃ i, nhÆ°ng ta khÃ´ng gÃ¡n IG vÃ o nÃ³ Ä‘Æ°á»£c, vÃ¬ IG lÃ  hai chiá»u ra vÃ o, trong khi ta chá»‰ muá»‘n 1 chiá»u thÃ´i lÃ  chiá»u tÆ°Æ¡ng tÃ¡c tá»« bÃªn trong private subnet ra bÃªn ngoÃ i vÃ  ta khÃ´ng muá»‘n cÃ³ chiá»u ngÆ°á»£c láº¡i.

### NAT gateway

ÄÃ¢y lÃ  tháº±ng giÃºp ta lÃ m viá»‡c Ä‘Ã³, ta sáº½ deploy NAT lÃªn trÃªn má»™t public subnet, vÃ  gÃ¡n nÃ³ vÃ o route table, rá»“i gÃ¡n route table Ä‘Ã³ vÃ o cÃ¡c private subnet.

![](https://images.viblo.asia/bb0564cc-f693-4ee4-b4bd-04b3e7e3234a.jpg)

Ta dÃ¹ng resourceÂ `aws_nat_gateway`Â cá»§a Terraform Ä‘á»ƒ táº¡o NAT.

```
...
resource "aws_eip" "nat" {
  vpc = true
}

resource "aws_nat_gateway" "public" {
  depends_on = [aws_internet_gateway.ig]

  allocation_id = aws_eip.nat.id
  subnet_id     = aws_subnet.public_subnet[0].id

  tags = {
    Name = "Public NAT"
  }
}

```

Táº¡o private route table vÃ  gÃ¡n NAT vÃ o nÃ³.

```
...
resource "aws_route_table" "private" {
  vpc_id = aws_vpc.vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_nat_gateway.public.id
  }

  tags = {
    "Name" = "private"
  }
}

```

GÃ¡n route table vÃ o cÃ¡c private subnet.

```
...
resource "aws_route_table_association" "public_private" {
  for_each       = { for k, v in aws_subnet.private_subnet : k => v }
  subnet_id      = each.value.id
  route_table_id = aws_route_table.private.id
}

```

Full code.

```
provider "aws" {
  region  = "us-west-2"
}

resource "aws_vpc" "vpc" {
  cidr_block           = "10.0.0.0/16"
  enable_dns_hostnames = true

  tags = {
    "Name" = "custom"
  }
}

locals {
  private = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  public  = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]
  zone    = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

resource "aws_subnet" "private_subnet" {
  count = length(local.private)

  vpc_id            = aws_vpc.vpc.id
  cidr_block        = local.private[count.index]
  availability_zone = local.zone[count.index % length(local.zone)]

  tags = {
    "Name" = "private-subnet"
  }
}

resource "aws_subnet" "public_subnet" {
  count = length(local.public)

  vpc_id            = aws_vpc.vpc.id
  cidr_block        = local.public[count.index]
  availability_zone = local.zone[count.index % length(local.zone)]

  tags = {
    "Name" = "public-subnet"
  }
}

resource "aws_internet_gateway" "ig" {
  vpc_id = aws_vpc.vpc.id

  tags = {
    "Name" = "custom"
  }
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.ig.id
  }

  tags = {
    "Name" = "public"
  }
}

resource "aws_route_table_association" "public_association" {
  for_each       = { for k, v in aws_subnet.public_subnet : k => v }
  subnet_id      = each.value.id
  route_table_id = aws_route_table.public.id
}

resource "aws_eip" "nat" {
  vpc = true
}

resource "aws_nat_gateway" "public" {
  depends_on = [aws_internet_gateway.ig]

  allocation_id = aws_eip.nat.id
  subnet_id     = aws_subnet.public_subnet[0].id

  tags = {
    Name = "Public NAT"
  }
}

resource "aws_route_table" "private" {
  vpc_id = aws_vpc.vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_nat_gateway.public.id
  }

  tags = {
    "Name" = "private"
  }
}

resource "aws_route_table_association" "public_private" {
  for_each       = { for k, v in aws_subnet.private_subnet : k => v }
  subnet_id      = each.value.id
  route_table_id = aws_route_table.private.id
}

```

Oke, váº­y lÃ  ta Ä‘Ã£ viáº¿t code xong, tiáº¿p theo ta cháº¡y cÃ¢u lá»‡nh apply Ä‘á»ƒ táº¡o háº¡ táº§ng nÃ o ğŸ˜.

```
$ terraform apply -auto-approve
...
Plan: 18 to add, 0 to change, 0 to destroy.
...
Apply complete! Resources: 18 added, 0 changed, 0 destroyed.

```

NhÆ° ta tháº¥y thÃ¬ sá»­ dá»¥ng Terraform Ä‘á»ƒ táº¡o VPC cÅ©ng khÃ¡ Ä‘Æ¡n giáº£n, cÃ³ cÃ¡i lÃ  viáº¿t code hÆ¡i nhiá»u, váº­y má»—i láº§n ta muá»‘n táº¡o má»™t VPC khÃ¡c khÃ´ng láº» ta pháº£i copy Ä‘á»‘ng code nÃ y qua chá»— khÃ¡c tiáº¿p, váº­y thÃ¬ báº¥t tiá»‡n quÃ¡. Äá»ƒ giáº£i quyáº¿t váº¥n Ä‘á» nÃ y thÃ¬ Terraform cÃ³ cung cáº¥p cho ta má»™t tÃ­nh nÄƒng lÃ  module, giÃºp ta gom code láº¡i thÃ nh má»™t module vÃ  cÃ³ thá»ƒ sá»­ dá»¥ng láº¡i á»Ÿ nhiá»u chá»— khÃ¡c nhau. Nhá»› destroy resource nhÃ©.

```
terraform destroy -auto-approve

```

Terraform Module
----------------

Terraform Module lÃ  má»™t self-contained packages of code, cho phÃ©p ta gom code láº¡i má»™t chá»— vÃ  sá»­ dá»¥ng á»Ÿ nhiá»u chá»— khÃ¡c nhau. Khi nÃ³i vá» module, ta cÃ³ thá»ƒ nghÄ© nÃ³ nhÆ° lÃ  má»™t blocks nhá» trong má»™t sáº£n pháº©m lá»›n, ta sáº½ gÃ¡p nhiá»u blocks nhá» nÃ y láº¡i vá»›i nhau Ä‘á»ƒ ra Ä‘Æ°á»£c sáº£n pháº©m cuá»‘i cÃ¹ng, vÃ  blocks nÃ y ta cÃ³ thá»ƒ láº¥y Ä‘á»ƒ xÃ¢y dá»±ng sáº£n pháº©m khÃ¡c cÅ©ng Ä‘Æ°á»£c, nhÆ° trÃ² chÆ¡i lego.

### Standard module structure

Má»™t module cÆ¡ báº£n sáº½ gá»“m 3 file sau Ä‘Ã¢y:

-   `main.tf`Â chá»©a code.
-   `variables.tf`Â chá»©a input vÃ o cá»§a module.
-   `outputs.tf`Â chÆ°a giÃ¡ trá»‹ Ä‘áº§u ra cá»§a module.

NgoÃ i ra cÃ²n má»™t vÃ i file khÃ¡c mÃ  khÃ´ng báº¯t buá»™c lÃ Â `providers.tf`,Â `versions.tf`Â cÃ¡c báº¡n xem full cáº¥u trÃºc á»Ÿ trang nÃ yÂ <https://www.terraform.io/language/modules/develop#standard-module-structure>.

### Using module

Äá»ƒ sá»­ dá»¥ng module, ta dÃ¹ng resource tÃªn lÃ  module nhÆ° sau.

```
module <module_name> {
  source = <source>
  version = <version>

  input_one = <input_one>
  input_two = <input_two>
}

```

`<source>`Â cÃ³ thá»ƒ lÃ  dÆ°á»ng dáº«n local hoáº·c má»™t url trÃªn máº¡ng,Â `<version>`Â chá»‰ Ä‘á»‹nh version cá»§a module,Â `<input_one>`Â lÃ  cÃ¡c giÃ¡ trá»‹ input ta Ä‘á»‹nh nghÄ©a trong fileÂ `variables.tf`.

### Write module

BÃ¢y giá» ta sáº½ chuyá»ƒn code mÃ  ta dÃ¹ng Ä‘á»ƒ táº¡o VPC á»Ÿ trÃªn thÃ nh module, trÆ°á»›c khi viáº¿t module ta cáº§n pháº£i Ä‘á»‹nh nghÄ©a trÆ°á»›c nhá»¯ng giÃ¡ trá»‹ nÃ o mÃ  ta cáº§n nÃ³ Ä‘á»™ng á»Ÿ trong module, Ä‘á»ƒ khi ta sá»­ dá»¥ng module ta sáº½ truyá»n giÃ¡ trá»‹ Ä‘Ã³ vÃ o Ä‘á»ƒ cÃ³ Ä‘Æ°á»£c cÃ¡c resource khÃ¡c nhau tÃ¹y thuá»™c vÃ o biáº¿n ta truyá»n vÃ o.

VÃ­ dá»¥ á»Ÿ trÃªn, thÃ¬ cÃ¡c giÃ¡ trá»‹ Ä‘á»™ng mÃ  ta cáº§n truyá»n vÃ o module vpc cá»§a ta lÃ :

-   vpc cidr_block
-   subnet cidr_block vÃ  zone.

Ta táº¡o folder vá»›i cáº¥u trÃºc nhÆ° sau.

```
.
â”œâ”€â”€ main.tf
â””â”€â”€ vpc
    â”œâ”€â”€ main.tf
    â”œâ”€â”€ outputs.tf
    â””â”€â”€ variables.tf

```

Ta Ä‘á»‹nh nghÄ©a giÃ¡ trá»‹ input cá»§a module á»Ÿ trong fileÂ `variables.tf`Â nhÆ° sau.

```
variable "vpc_cidr_block" {
  type    = string
  default = "10.0.0.0/16"
}

variable "private_subnet" {
  type    = list(string)
}

variable "public_subnet" {
  type    = list(string)
}

variable "availability_zone" {
  type    = list(string)
}

```

Cáº­p nháº­t code cho fileÂ `main.tf`Â á»Ÿ trong vpc.

```
resource "aws_vpc" "vpc" {
  cidr_block           = var.vpc_cidr_block
  enable_dns_hostnames = true

  tags = {
    "Name" = "custom"
  }
}

resource "aws_subnet" "private_subnet" {
  count = length(var.private_subnet)

  vpc_id            = aws_vpc.vpc.id
  cidr_block        = var.private_subnet[count.index]
  availability_zone = var.availability_zone[count.index % length(var.availability_zone)]

  tags = {
    "Name" = "private-subnet"
  }
}

resource "aws_subnet" "public_subnet" {
  count = length(var.public_subnet)

  vpc_id            = aws_vpc.vpc.id
  cidr_block        = var.public_subnet[count.index]
  availability_zone = var.availability_zone[count.index % length(var.availability_zone)]

  tags = {
    "Name" = "public-subnet"
  }
}

resource "aws_internet_gateway" "ig" {
  vpc_id = aws_vpc.vpc.id

  tags = {
    "Name" = "custom"
  }
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.ig.id
  }

  tags = {
    "Name" = "public"
  }
}

resource "aws_route_table_association" "public_association" {
  for_each       = { for k, v in aws_subnet.public_subnet : k => v }
  subnet_id      = each.value.id
  route_table_id = aws_route_table.public.id
}

resource "aws_eip" "nat" {
  vpc = true
}

resource "aws_nat_gateway" "public" {
  depends_on = [aws_internet_gateway.ig]

  allocation_id = aws_eip.nat.id
  subnet_id     = aws_subnet.public_subnet[0].id

  tags = {
    Name = "Public NAT"
  }
}

resource "aws_route_table" "private" {
  vpc_id = aws_vpc.vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_nat_gateway.public.id
  }

  tags = {
    "Name" = "private"
  }
}

resource "aws_route_table_association" "public_private" {
  for_each       = { for k, v in aws_subnet.private_subnet : k => v }
  subnet_id      = each.value.id
  route_table_id = aws_route_table.private.id
}

```

NhÆ° ta tháº¥y, giÃ¡ trá»‹ fix cá»©ng ta Ä‘Ã£ thay Ä‘á»•i thÃ nh variable, khi ta xÃ i module vpc nÃ y ta chá»‰ cáº§n truyá»n vÃ o giÃ¡ trá»‹ input khÃ¡c ta sáº½ cÃ³ Ä‘Æ°á»£c vpc khÃ¡c. á» trong fileÂ `main.tf`Â ngoÃ i cÃ¹ng, ta sá»­ dá»¥ng module nhÆ° sau.

```
provider "aws" {
  region = "us-west-2"
}

module "vpc" {
  source = "./vpc"

  vpc_cidr_block    = "10.0.0.0/16"
  private_subnet    = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  public_subnet     = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]
  availability_zone = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

```

Oke, nhÆ° báº¡n tháº¥y thÃ¬ code cá»§a ta khi xÃ i module nÃ³ ráº¥t lÃ  gá»n, ta cháº¡y thá»­ cÃ¢u lá»‡nh plan Ä‘á»ƒ xem module cá»§a ta cÃ³ viáº¿t Ä‘Ãºng hay khÃ´ng.

```
$ terraform plan
...
Plan: 18 to add, 0 to change, 0 to destroy.
...

```

Náº¿u nÃ³ in ra Ä‘Æ°á»£c dÃ²ng á»Ÿ trÃªn thÃ¬ module cá»§a ta Ä‘Ã£ viáº¿t Ä‘Ãºng nhÃ©, cÃ¡c báº¡n cÃ³ thá»ƒ cháº¡y apply Ä‘á»ƒ xem.

### Publish module

Tiáº¿p theo ta sáº½ Ä‘áº©y module cá»§a ta lÃªn trÃªn máº¡ng Ä‘á»ƒ má»i ngÆ°á»i cÃ³ thá»ƒ sá»­ dá»¥ng, Ä‘á»ƒ táº¡o module thÃ¬ ta cáº§n pháº£i cÃ³ tÃ i khoáº£n github vÃ  truy cáº­p trangÂ [https://registry.terraform.io](https://registry.terraform.io/)Â login vÃ o nÃ³.

ÄÄƒng nháº­p github vÃ  táº¡o má»™t repo á»Ÿ tráº¡ng thÃ¡i public, tÃªn pháº£i á»Ÿ dáº¡ng formatÂ `terraform-<PROVIDER>-<NAME>`, sau Ä‘Ã³ copy 3 file á»Ÿ folder vpc vÃ  Ä‘áº©y lÃªn github repo Ä‘Ã³, vÃ­ dá»¥ cá»§a mÃ¬nh táº¡o má»™t repo tÃªn lÃ  terraform-aws-vpc.

![image.png](https://images.viblo.asia/a90708cc-b1c3-45b2-b3e4-294f39200314.png)

Sau Ä‘Ã³ ta cáº§n táº¡o tag cho repo nÃ y, tag nÃ y sáº½ tÆ°Æ¡ng á»©ng vá»›i version module cá»§a ta.

![image.png](https://images.viblo.asia/f4c8b16e-b3eb-4025-96fb-f84ea0372168.png)

Sau Ä‘Ã³ truy cáº­p trang registry á»Ÿ trÃªn. Khi báº¡n Ä‘Äƒng nháº­p rá»“i thÃ¬ nÃ³ sáº½ cÃ³ menu publish, ta nháº¥n vÃ o vÃ  chá»n module.

![image.png](https://images.viblo.asia/930ed8ee-0059-4445-a033-6bbba5618ecf.png)

Sau Ä‘Ã³ nÃ³ sáº½ dáº«n ta qua trang chá»n module Ä‘á»ƒ publish. Chá»n vpc module.

![image.png](https://images.viblo.asia/ad65e977-cf7e-4792-b472-6c0ef8cf155b.png)

Sau Ä‘Ã³ nháº¥n publish module, ta sáº½ tháº¥y module cá»§a ta.

![image.png](https://images.viblo.asia/9cfc9475-ff7a-4b22-ae68-8ea2fcc6fde1.png)

PhÃ­a bÃªn pháº£i cÃ³ Ä‘á»ƒ cÃ¡ch lÃ m sau Ä‘á»ƒ sá»­ dá»¥ng module nÃ y. Giá» náº¿u ta muá»‘n táº¡o VPC thÃ¬ ta sáº½ sá»­ dá»¥ng module nhÆ° sau.

```
module "vpc" {
  source  = "hoalongnatsu/vpc/aws"
  version = "1.0.0"

  vpc_cidr_block    = "10.0.0.0/16"
  private_subnet    = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  public_subnet     = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]
  availability_zone = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

```

Common module
-------------

á» trÃªn ta viáº¿t vá»›i má»¥c Ä‘Ã­ch lÃ  tÃ¬m hiá»ƒu, khi lÃ m thá»±c táº¿ cho mÃ´i trÆ°á»ng production, ta nÃªn xÃ i nhá»¯ng module cÃ³ sáºµn trÃªn máº¡ng, há» viáº¿t sáº½ kÄ© hÆ¡n nhiá»u vÃ  sáº½ cÃ³ ráº¥t nhiá»u chá»©c nÄƒng vÃ  use case hÆ¡n so vá»›i ta pháº£i tá»± viáº¿t. VÃ­ dá»¥ vpc module á»Ÿ trÃªn, ta cÃ³ thá»ƒ sá»­ dá»¥ng má»™t module cÃ³ sáºµn lÃ Â [terraform-aws-modules/vpc/aws](https://registry.terraform.io/modules/terraform-aws-modules/vpc/aws/latest).

![image.png](https://images.viblo.asia/e94b4539-e727-45d1-96dd-cc43dbe5808c.png)

Há» cover cho ta ráº¥t nhiá»u trÆ°á»ng há»£p. VÃ­ dá»¥ táº¡o má»™t VPC cho AWS Kubernetes dÃ¹ng module cÃ³ sáºµn.

```
module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "~> 3.0"

  name = var.cluster_name
  cidr = "10.0.0.0/16"

  azs              = ["${var.region}a", "${var.region}b", "${var.region}c"]
  private_subnets  = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  public_subnets   = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]
  database_subnets = ["10.0.7.0/24", "10.0.8.0/24"]

  enable_nat_gateway     = true
  single_nat_gateway     = true
  one_nat_gateway_per_az = false
  enable_dns_hostnames   = true

  // Create db subnet group and enable public access to RDS instances
  create_database_subnet_group           = true
  create_database_subnet_route_table     = true
  create_database_internet_gateway_route = true

  public_subnet_tags = {
    "kubernetes.io/cluster/${var.cluster_name}" = "shared"
    "kubernetes.io/role/elb"                    = 1
  }

  private_subnet_tags = {
    "kubernetes.io/cluster/${var.cluster_name}" = "shared"
    "kubernetes.io/role/internal-elb"           = 1
  }

  tags = local.tags
}

```

Vá»›i nhiá»u trÆ°á»ng há»£p nhÆ° trÃªn mÃ  ta tá»± code thÃ¬ cÅ©ng tá»‘i mÃ y tá»‘i máº·t, chÆ°a ká»ƒ pháº£i test rá»“i lÃ m háº±ng bÃ  láº±ng thá»© ná»¯a, ráº¥t máº¥t thá»i gian ğŸ˜‚. NÃªn trÆ°á»›c khi ta lÃ m gÃ¬ thÃ¬ lÃªn kiáº¿m xem coi cÃ³ ai viáº¿t module Ä‘Ã³ rá»“i chÆ°a nhÃ©, sáº½ giÃºp ta tiáº¿t kiá»‡m ráº¥t nhiá»u thá»i gian. Github repo cá»§a toÃ n bá»™ seriesÂ <https://github.com/hoalongnatsu/terraform-series>.

Káº¿t luáº­n
--------

Váº­y lÃ  ta Ä‘Ã£ tÃ¬m hiá»ƒu xong cÃ¡ch viáº¿t code tá»« Ä‘áº§u vÃ  sau Ä‘Ã³ tá»• chá»©c code láº¡i thÃ nh module, cÃ¡ch publish má»™t module lÃªn trÃªn máº¡ng, cÃ¡ch sá»­ dá»¥ng module cÃ³ sáºµn. Module giÃºp ta sá»­ dá»¥ng code cÃ³ sáºµn vÃ  trÃ¡nh pháº£i viáº¿t code Ä‘i code láº¡i nhiá»u láº§n.