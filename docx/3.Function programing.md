Terraform h·ªó tr·ª£ ta l·∫≠p tr√¨nh theo c√°ch functional programming, v√† ta s·∫Ω n√≥i qua c√°ch s·ª≠ d·ª•ng c√°c function trong terraform, expressions x√†i ra sao, ...

Provisioning EC2
----------------

Ta s·∫Ω l√†m v√≠ d·ª• v·ªÅ EC2 ƒë·ªÉ t√¨m hi·ªÉu v·ªÅ c√°c kh√°i ni·ªám tr√™n. T·∫°o m·ªôt file t√™n l√† `main.tf`

    provider "aws" {
      region = "us-west-2"
    }
    
    data "aws_ami" "ubuntu" {
      most_recent = true
    
      filter {
        name   = "name"
        values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
      }
    
      owners = ["099720109477"]
    }
    
    resource "aws_instance" "hello" {
      ami           = data.aws_ami.ubuntu.id
      instance_type = "t2.micro"
    }
    

Ch·∫°y `terraform init` v√† `terraform apply`, sau ƒë√≥ l√™n AWS ta s·∫Ω th·∫•y EC2 c·ªßa ta. V·ªõi ƒëo·∫°n code tr√™n th√¨ EC2 c·ªßa ta lu√¥n lu√¥n c√≥ instance\_type l√† t2.micro, v√† n·∫øu ta mu·ªën t·∫°o l·∫°i EC2 m√† s·∫Ω c√≥ instance\_type kh√°c m·∫°nh h∆°n th√¨ l√†m sao? Ta s·∫Ω s·ª≠a l·∫°i code trong file terraform? V·∫≠y th√¨ kh√¥ng linh ho·∫°t cho l·∫Øm, m√† thay v√†o ƒë√≥ ta s·∫Ω s·ª≠ d·ª•ng variable ƒë·ªÉ l√†m vi·ªác n√†y.

### Input Variables

Ta c√≥ th·ªÉ ƒë·ªãnh nghƒ©a variable b√™n trong terraform, v·ªõi c√∫ ph√°p nh∆∞ sau.
![alt](../images/funtion1.png) 

Ta s·∫Ω d√πng variable block ƒë·ªÉ khai b√°o variable, v√† theo sau n√≥ l√† t√™n c·ªßa variable ƒë√≥. ·ªû v√≠ d·ª• tr√™n, ta t·∫°o th√™m m·ªôt file n·ªØa v·ªõi t√™n l√† `variable.tf` (n√†y b·∫°n ƒë·∫∑t t√™n g√¨ c≈©ng ƒë∆∞·ª£c nha) ƒë·ªÉ khai b√°o bi·∫øn c·ªßa ta.

    variable "instance_type" {
      type = string
      description = "Instance type of the EC2"
    }
    

Thu·ªôc t√≠nh l√† type ƒë·ªÉ ch·ªâ ƒë·ªãnh type c·ªßa bi·∫øn ƒë√≥, thu·ªôc t√≠nh description d√πng ƒë·ªÉ ghi l·∫°i m√¥ t·∫£ cho ng∆∞·ªùi ƒë·ªçc bi·∫øn ƒë√≥ c√≥ √Ω nghƒ©a g√¨. **Ch·ªâ c√≥ thu·ªôc t√≠nh type l√† b·∫Øt bu·ªôc ph·∫£i khai b√°o**. Trong terraform th√¨ m·ªôt bi·∫øn s·∫Ω c√≥ c√°c type sau ƒë√¢y:

*   Basic type: string, number, bool
*   Complex type: list(), set(), map(), object(), tuple()

> Trong terraform, type number v√† type bool s·∫Ω ƒë∆∞·ª£c convert th√†nh type string khi c·∫ßn thi·∫øt. Nghƒ©a l√† 1 s·∫Ω th√†nh "1", true s·∫Ω th√†nh "true"

ƒê·ªÉ truy c·∫≠p ƒë∆∞·ª£c gi√° tr·ªã c·ªßa variable th√¨ ta s·∫Ω d√πng theo c√∫ ph√°p sau `var.<VARIABLE_NAME>`, c·∫≠p nh·∫≠t l·∫°i file `main.tf`

    provider "aws" {
      region = "us-west-2"
    }
    
    data "aws_ami" "ubuntu" {
      most_recent = true
    
      filter {
        name   = "name"
        values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
      }
    
      owners = ["099720109477"]
    }
    
    resource "aws_instance" "hello" {
      ami           = data.aws_ami.ubuntu.id
      instance_type = var.instance_type # change here
    }
    

·ªû thu·ªôc t√≠nh instance\_type thay v√¨ g√°n c·ª©ng th√¨ b√¢y gi·ªù ta s·∫Ω d√πng bi·∫øn **var.instance\_type**.

### G√°n gi√° tr·ªã cho variable

ƒê·ªÉ g√°n gi√° tr·ªã cho bi·∫øn, ta s·∫Ω t·∫°o m·ªôt file t√™n l√† terraform.tfvars

    instance_type = "t2.micro"
    

Khi ta ch·∫°y `terraform apply` th√¨ file terraform.tfvars s·∫Ω ƒë∆∞·ª£c terraform s·ª≠ d·ª•ng m·∫∑c ƒë·ªãnh ƒë·ªÉ load gi√° tr·ªã cho bi·∫øn, n·∫øu ta kh√¥ng mu·ªën d√πng m·∫∑c ƒë·ªãnh, th√¨ khi ch·∫°y c√¢u l·ªánh apply ta th√™m v√†o option l√† -var-file n·ªØa. T·∫°o m·ªôt file t√™n l√† production.tfvars

    instance_type = "t3.small"
    

Khi ch·∫°y CI/CD cho production, ta ch·ªâ ƒë·ªãnh file variable nh∆∞ sau:

    terraform apply -var-file="production.tfvars"
    

B√¢y gi·ªù th√¨ gi√° tr·ªã instance\_type c·ªßa ta s·∫Ω linh ho·∫°t h∆°n nhi·ªÅu.

### Validating variables

Ta c≈©ng c√≥ th·ªÉ ƒë·ªãnh nghƒ©a bi·∫øn n√†y ch·ªâ c√≥ th·ªÉ ƒë∆∞·ª£c g√°n nh·ªØng gi√° tr·ªã m√† ta cho ph√©p b·∫±ng c√°ch s·ª≠ d√πng thu·ªôc t√≠nh validating, nh∆∞ sau:

    variable "instance_type" {
      type = string
      description = "Instance type of the EC2"
    
      validation {
        condition = contains(["t2.micro", "t3.small"], var.instance_type)
        error_message = "Value not allow."
      }
    }
    

·ªû file tr√™n ta s·∫Ω d√πng function contains ƒë·ªÉ ki·ªÉm tra gi√° tr·ªã c·ªßa bi·∫øn instance\_type n√†y ch·ªâ ƒë∆∞·ª£c n·∫±m trong m·∫£ng array ta cho ph√©p, n·∫øu kh√¥ng th√¨ khi ta ch·∫°y c√¢u l·ªánh apply b·∫°n s·∫Ω th·∫•y l·ªói l√† ·ªü tr∆∞·ªùng error\_message ƒë∆∞·ª£c in ra. S·ª≠a l·∫°i file terraform.tfvars

    instance_type = "t3.micro"
    

Ch·∫°y terraform apply.

    $ terraform apply
    ‚ï∑
    ‚îÇ Error: Invalid value for variable
    ‚îÇ 
    ‚îÇ   on variable.tf line 1:
    ‚îÇ    1: variable "instance_type" {
    ‚îÇ 
    ‚îÇ Value not allow.
    ‚îÇ 
    ‚îÇ This was checked by the validation rule at variable.tf:5,3-13.
    ‚ïµ
    

S·ª≠ d·ª•ng validating ƒë·ªÉ ki·ªÉm so√°t gi√° tr·ªã c·ªßa bi·∫øn m√† b·∫°n mu·ªën. S·ª≠a l·∫°i file terraform.tfvars nh∆∞ c≈© nh√©. Th√¥ng th∆∞·ªùng khi t·∫°o EC2 xong, ta s·∫Ω mu·ªën xem ƒë·ªãa ch·ªâ IP c·ªßa n√≥, ƒë·ªÉ l√†m ƒë∆∞·ª£c vi·ªác ƒë√≥ th√¨ ta s·ª≠ d·ª•ng output block.

### Output

Gi√° tr·ªã c·ªßa output block s·∫Ω ƒë∆∞·ª£c in ra terminal, c√∫ ph√°p c·ªßa output nh∆∞ sau.

![alt](../images/funtion2.png)

ƒê·ªÉ in ƒë∆∞·ª£c gi√° tr·ªã public IP c·ªßa EC2, ta th√™m v√†o file `main.tf` ƒëo·∫°n code sau:

    ...
    
    output "ec2" {
      value = {
        public_ip = aws_instance.hello.public_ip
      }
    }
    

B·∫°n ch·∫°y l·∫°i c√¢u l·ªánh apply, ta s·∫Ω th·∫•y gi√° tr·ªã IP c·ªßa EC2 ƒë∆∞·ª£c in ra terminal.

    $ terraform apply -auto-approve
    ...
    
    Apply complete! Resources: 1 added, 0 changed, 0 destroyed.
    
    Outputs:
    
    ec2 = {
      "public_ip" = "52.36.124.230"
    }
    

Oke, b√¢y gi·ªù th√¨ ta ƒë√£ bi·∫øt c√°ch s·ª≠ d·ª•ng varibale v√† output. Ti·∫øp theo, n·∫øu ta mu·ªën th√™m m·ªôt EC2 n·ªØa th√¨ sao? Trong file `main.tf` ta s·∫Ω copy ra th√™m m·ªôt EC2 n·ªØa, nh∆∞ sau:

    provider "aws" {
      region = "us-west-2"
    }
    
    data "aws_ami" "ubuntu" {
      most_recent = true
    
      filter {
        name   = "name"
        values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
      }
    
      owners = ["099720109477"]
    }
    
    resource "aws_instance" "hello1" {
      ami           = data.aws_ami.ubuntu.id
      instance_type = var.instance_type
    }
    
    resource "aws_instance" "hello2" {
      ami           = data.aws_ami.ubuntu.id
      instance_type = var.instance_type
    }
    
    output "ec2" {
      value = {
        public_ip1 = aws_instance.hello1.public_ip
        public_ip2 = aws_instance.hello2.public_ip
      }
    }
    

Ta s·∫Ω th√™m m·ªôt resource block cho EC2 th·ª© hai, v√† ·ªü ph·∫ßn output, ta c·∫≠p nh·∫≠t l·∫°i ƒë·ªÉ n√≥ c√≥ th·ªÉ in ra ƒë∆∞·ª£c IP c·ªßa hai con EC2. M·ªçi th·ª© ƒë·ªÅu kh√¥ng c√≥ g√¨ ph·ª©c t·∫°p h·∫øt, nh∆∞ng n·∫øu gi·ªù ta mu·ªën 100 con EC2 th√¨ sao? Ta c√≥ th·ªÉ copy ra 100 resource block, nh∆∞ng kh√¥ng ai l√†m v·∫≠y üòÇ, m√† ta s·∫Ω s·ª≠ d·ª•ng count parameter.

### Count parameter

Count l√† m·ªôt **meta argument**, l√† m·ªôt thu·ªôc t√≠nh trong terraform ch·ª© kh√¥ng ph·∫£i c·ªßa resource type thu·ªôc provider, ·ªü b√†i 1 ta ƒë√£ n√≥i resource type block ch·ªâ c√≥ ch·ª©a c√°c thu·ªôc t√≠nh m√† provider cung c·∫•p cho, c√≤n meta argument l√† thu·ªôc t√≠nh c·ªßa terraform => nghƒ©a l√† ta c√≥ th·ªÉ s·ª≠ d·ª•ng n√≥ ·ªü b·∫•t k√¨ resource block n√†o. C·∫≠p nh·∫≠t l·∫°i file `main.tf` m√† s·∫Ω t·∫°o ra 5 EC2 nh∆∞ sau:

    provider "aws" {
      region = "us-west-2"
    }
    
    data "aws_ami" "ubuntu" {
      most_recent = true
    
      filter {
        name   = "name"
        values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
      }
    
      owners = ["099720109477"]
    }
    
    resource "aws_instance" "hello" {
      count         = 5
      ami           = data.aws_ami.ubuntu.id
      instance_type = var.instance_type
    }
    
    output "ec2" {
      value = {
        public_ip1 = aws_instance.hello[0].public_ip
        public_ip2 = aws_instance.hello[1].public_ip
        public_ip3 = aws_instance.hello[2].public_ip
        public_ip4 = aws_instance.hello[3].public_ip
        public_ip5 = aws_instance.hello[4].public_ip
      }
    }
    

B√¢y gi·ªù th√¨ khi ta ch·∫°y apply, terraform s·∫Ω t·∫°o ra cho ta 5 EC2. B·∫°n s·∫Ω ƒë·ªÉ √Ω l√† ·ªü ph·∫ßn output, ƒë·ªÉ truy c·∫≠p ƒë∆∞·ª£c resource, th√¨ ta s·∫Ω d√πng th√™m d·∫•u `[]` v√† gi√° tr·ªã index c·ªßa resource. B√¨nh th∆∞·ªùng, ƒë·ªÉ truy c·∫≠p ƒë∆∞·ª£c resource, ta d√πng theo c√∫ ph√°p `<RESOURCE TYPE>.<NAME>`, nh∆∞ng khi ta d√πng count th√¨ ta s·∫Ω truy c·∫≠p resource theo c√∫ ph√°p sau `<RESOURCE TYPE>.<NAME>[index]`.

B√¢y gi·ªù ta ƒë√£ gi·∫£i quy·∫øt ƒë∆∞·ª£c v·∫•n ƒë·ªÅ copy resource ra khi c·∫ßn t·∫°o n√≥ v·ªõi s·ªë l∆∞·ª£ng nhi·ªÅu h∆°n, nh∆∞ng ·ªü ph·∫ßn output, ta v·∫´n ph·∫£i ghi ra t·ª´ng resource ri√™ng l·∫ª. Ta s·∫Ω gi·∫£i quy·∫øt n√≥ b·∫±ng c√°ch s·ª≠ d·ª•ng for expressions.

### For expressions

For cho ph√©p ta duy·ªát qua m·ªôt list, c√∫ ph√°p c·ªßa l·ªánh for nh∆∞ sau:

    for <value> in <list> : <return value>
    

V√≠ d·ª• d√πng for:

*   T·∫°o ra m·ªôt array m·ªõi v·ªõi gi√° tr·ªã c·ªßa array m·ªõi s·∫Ω ƒë∆∞·ª£c upper: `[for s in var.words : upper(s)]`
*   T·∫°o ra m·ªôt object m·ªõi v·ªõi value c·ªßa object ƒë∆∞·ª£c upper: `{ for k, v in var.words : k => upper(s) }`

Ta s·∫Ω d√πng for ƒë·ªÉ r√∫t g·ªçn ph·∫ßn output IP c·ªßa EC2. C·∫≠p nh·∫≠t l·∫°i file `main.tf`

    ...
    
    resource "aws_instance" "hello" {
      count         = 5
      ami           = data.aws_ami.ubuntu.id
      instance_type = var.instance_type
    }
    
    output "ec2" {
      value = {
        public_ip = [ for v in aws_instance.hello : v.public_ip ]
      }
    }
    

Ph·∫ßn output tr√™n s·∫Ω in ra cho ta gi√° tr·ªã public\_ip l√† m·ªôt m·∫£ng IP c·ªßa t·∫•t c·∫£ EC2 ƒë∆∞·ª£c t·∫°o ra. C√≤n n·∫øu b·∫°n mu·ªën in output ra theo ki·ªÉu `{ public_ip1: <value>, public_ip2: <value> }` th√¨ ta c√≥ th·ªÉ d√πng format function.

### Format function

H√†m format s·∫Ω gi√∫p ta n·ªëi chu·ªói theo d·∫°ng ta mu·ªën, c·∫≠p nh·∫≠t output l·∫°i nh∆∞ sau:

    ...
    
    resource "aws_instance" "hello" {
      count         = 5
      ami           = data.aws_ami.ubuntu.id
      instance_type = var.instance_type
    }
    
    output "ec2" {
      value = { for i, v in aws_instance.hello : format("public_ip%d", i + 1) => v.public_ip }
    }
    

Khi b·∫°n terraform plan ƒë·ªÉ ki·ªÉm tra, s·∫Ω th·∫•y output l√∫c n√†y s·∫Ω l√† d·∫°ng `{ public_ip1: <value>, public_ip2: <value> }`.

    $ terraform plan
    ...
    Changes to Outputs:
      + ec2 = {
          + public_ip1 = (known after apply)
          + public_ip2 = (known after apply)
          + public_ip3 = (known after apply)
          + public_ip4 = (known after apply)
          + public_ip5 = (known after apply)
        }
    
    ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ
    
    Note: You didn't use the -out option to save this plan, so Terraform can't guarantee to take exactly these actions if you
    run "terraform apply" now.
    

T·ªõi ƒë√¢y th√¨ ta ƒë√£ bi·∫øt ƒë∆∞·ª£c c√°ch x√†i m·ªôt s·ªë c√∫ ph√°p ƒë∆°n gi·∫£n, ti·∫øp theo m√¨nh s·∫Ω chuy·ªÉn sang v√≠ d·ª• v·ªÅ S3 ƒë·ªÉ n√≥i th√™m v·ªÅ m·ªôt s·ªë function hay ƒë∆∞·ª£c s·ª≠ d·ª•ng n·ªØa.

Provisioning S3
---------------

·ªû v√≠ d·ª• n√†y ta s·∫Ω t·∫°o m·ªôt S3 bucket ƒë·ªÉ host m·ªôt static website.
![alt](../images/funtion3.png)

T·∫°o folder m·ªõi v√† t·∫°o m·ªôt file `main.tf`

    provider "aws" {
      region = "us-west-2"
    }
    
    resource "aws_s3_bucket" "static" {
      bucket = "terraform-series-bai3"
      acl    = "public-read"
      policy = <<POLICY
      {
        "Version": "2012-10-17",
        "Statement": [
          {
            "Sid": "PublicReadGetObject",
            "Effect": "Allow",
            "Principal": "*",
            "Action": [
              "s3:GetObject"
            ],
            "Resource": [
              "arn:aws:s3:::terraform-series-bai3/*"
            ]
          }
        ]
      }
      POLICY
    
      website {
        index_document = "index.html"
        error_document = "error.html"
      }
    }
    

Ch·∫°y `terraform init` v√† `terraform apply`, sau ƒë√≥ b·∫°n s·∫Ω th·∫•y S3 bucket c·ªßa ta tr√™n AWS.

·ªû file tr√™n b·∫°n s·∫Ω th·∫•y l√† ph·∫ßn policy n√≥ h∆°i d√†i, v√† n√≥ l√† d·∫°ng chu·ªói json, n√™n config file c·ªßa ta h∆°i kh√≥ nh√¨n, ta c√≥ th·ªÉ t√°ch ph·∫ßn policy ra m·ªôt file json ri√™ng, v√† d√πng function trong terrafrom ƒë·ªÉ import file policy ƒë√≥ v√†o config file c·ªßa ta.

### File function

File function s·∫Ω gi√∫p ta t·∫£i n·ªôi dung c·ªßa m·ªôt file n√†o ƒë√≥ v√†o b√™n trong config file c·ªßa terraform. T·∫°o m·ªôt file t√™n l√† s3\_static\_policy.json v√† copy ƒëo·∫°n json tr√™n v√†o.

    {
      "Version": "2012-10-17",
      "Statement": [
        {
          "Sid": "PublicReadGetObject",
          "Effect": "Allow",
          "Principal": "*",
          "Action": [
            "s3:GetObject"
          ],
          "Resource": [
            "arn:aws:s3:::terraform-series-bai3/*"
          ]
        }
      ]
    }
    

C·∫≠p nh·∫≠t l·∫°i `main.tf`

    provider "aws" {
      region = "us-west-2"
    }
    
    resource "aws_s3_bucket" "static" {
      bucket = "terraform-series-bai3"
      acl    = "public-read"
      policy = file("s3_static_policy.json")
    
      website {
        index_document = "index.html"
        error_document = "error.html"
      }
    }
    

Nh∆∞ b·∫°n th·∫•y th√¨ khi ƒë·ªÉ policy v√†o m·ªôt file kh√°c v√† d√πng file function import v√†o, th√¨ file terraform c·ªßa ta nh√¨n g·ªçn h∆°n nhi·ªÅu. Ti·∫øp theo ta s·∫Ω ti·∫øn h√†nh upload file l√™n s3 bucket v√† truy v·∫≠p v√†o URL th√¨ ta s·∫Ω th·∫•y ƒë∆∞·ª£c trang web c·ªßa ta. Trang web m√† ta host s·∫Ω c√≥ giao di·ªán nh∆∞ sau.
![alt](../images/funtion4.png)
C√°c b·∫°n clone source code ·ªü ƒë√¢y nh√© [https://github.com/hoalongnatsu/static-web.git](https://github.com/hoalongnatsu/static-web.git), sau khi clone xong th√¨ nh·ªõ x√≥a file .git ƒëi.

    rm -rf static-web/.git
    

Th∆∞ m·ª•c c·ªßa ta hi·ªán t·∫°i s·∫Ω nh∆∞ sau.

    .
    ‚îú‚îÄ‚îÄ main.tf
    ‚îú‚îÄ‚îÄ s3_static_policy.json
    ‚îú‚îÄ‚îÄ static-web
    ‚îÇ   ‚îú‚îÄ‚îÄ README.md
    ‚îÇ   ‚îú‚îÄ‚îÄ article-details.html
    ...
    ‚îú‚îÄ‚îÄ terraform.tfstate
    

ƒê·ªÉ upload file l√™n s3 th√¨ ta s·∫Ω d√πng resource type l√† **aws\_s3\_bucket\_object**. C·∫≠p nh·∫≠t l·∫°i file `main.tf`

    provider "aws" {
      region = "us-west-2"
    }
    
    resource "aws_s3_bucket" "static" {
      bucket = "terraform-series-bai3"
      acl    = "public-read"
      policy = file("s3_static_policy.json")
    
      website {
        index_document = "index.html"
        error_document = "error.html"
      }
    }
    
    locals {
      mime_types = {
        html  = "text/html"
        css   = "text/css"
        ttf   = "font/ttf"
        woff  = "font/woff"
        woff2 = "font/woff2"
        js    = "application/javascript"
        map   = "application/javascript"
        json  = "application/json"
        jpg   = "image/jpeg"
        png   = "image/png"
        svg   = "image/svg+xml"
        eot   = "application/vnd.ms-fontobject"
      }
    }
    
    resource "aws_s3_bucket_object" "object" {
      for_each = fileset(path.module, "static-web/**/*")
      bucket = aws_s3_bucket.static.id
      key    = replace(each.value, "static-web", "")
      source = each.value
      etag         = filemd5("${each.value}")
      content_type = lookup(local.mime_types, split(".", each.value)[length(split(".", each.value)) - 1])
    }
    

T·∫°m th·ªùi th√¨ c√°c b·∫°n ch∆∞a c·∫ßn hi·ªÉu code ph·∫ßn aws\_s3\_bucket\_object, ph·∫ßn m√¨nh mu·ªën gi·ªõi thi·ªáu ·ªü ƒë√¢y l√† fileset function, thay v√¨ ch·ªâ t·∫£i m·ªôt file, th√¨ fileset s·∫Ω t·∫£i to√†n b·ªô file trong th∆∞ m·ª•c ƒë√≥ l√™n theo d·∫°ng set.

### Fileset function

V√≠ d·ª• ta c√≥ th∆∞ m·ª•c nh∆∞ sau

    .
    ‚îú‚îÄ‚îÄ index.html
    ‚îú‚îÄ‚îÄ index.css
    

Th√¨ khi ta d√πng h√†m `fileset(path.module, "*")` ta s·∫Ω c√≥ ƒë∆∞·ª£c data set nh∆∞ sau:

    {
      "index.html": "index.html",
      "index.css" : "index.css"
    }
    

V·ªõi gi√° tr·ªã key v√† value l√† t√™n c·ªßa file.

### Local values

B·∫°n s·∫Ω th·∫•y c√≥ m·ªôt block n·ªØa t√™n l√† locals, ƒë√¢y l√† block gi√∫p ta khai b√°o m·ªôt gi√° tr·ªã local trong file terraform v√† c√≥ th·ªÉ s·ª≠ d·ª•ng l·∫°i ƒë∆∞·ª£c nhi·ªÅu l·∫ßn. C√∫ ph√°p nh∆∞ sau:
![alt](../images/funtion5.png)

Kh√¥ng gi·ªëng nh∆∞ variable block, ta c·∫ßn khai b√°o type, th√¨ locals block ta s·∫Ω g√°n th·∫≥ng gi√° tr·ªã cho n√≥. V√≠ d·ª• nh∆∞ sau:

    locals {
      one = 1
      two = 2
      name = "max"
      flag = true
    }
    

ƒê·ªÉ truy c·∫≠p gi√° tr·ªã local th√¨ ta d√πng c√∫ ph√°p `local.<KEY>`. Oke, sau khi ch·∫°y c√¢u l·ªánh `terraform apply` l·∫°i, b·∫°n truy c·∫≠p trang web v·ªõi url l√† `http://terraform-series-bai3.s3-website-us-west-2.amazonaws.com/` th√¨ s·∫Ω th·∫•y ƒë∆∞·ª£c trang web c·ªßa ta üòÅ. N·∫øu b·∫°n ƒë·∫∑t t√™n S3 bucket kh√°c th√¨ URL tr√™n s·∫Ω kh√°c nh√©.

K·∫øt lu·∫≠n
--------

V·∫≠y l√† ta ƒë√£ t√¨m hi·ªÉu xong v·ªÅ m·ªôt s·ªë c√°ch ƒë∆°n gi·∫£n ƒë·ªÉ l·∫≠p tr√¨nh ƒë∆∞·ª£c trong terraform. S·ª≠ d·ª•ng varibale ƒë·ªÉ ch·ª©a bi·∫øn, s·ª≠ d·ª•ng output ƒë·ªÉ show gi√° tr·ªã ra terminal, s·ª≠ d·ª•ng for ƒë·ªÉ duy·ªát qua m·∫£ng, s·ª≠ d·ª•ng locals ƒë·ªÉ l∆∞u gi√° tr·ªã v√† s·ª≠ d·ª•ng l·∫°i nhi·ªÅu l·∫ßn.