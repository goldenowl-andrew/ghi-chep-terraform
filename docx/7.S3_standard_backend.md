á» bÃ i nÃ y chÃºng ta sáº½ thá»±c hÃ nh sá»­ dá»¥ng Terraform Standard Backend, cá»¥ thá»ƒ lÃ  S3 Standard Backend. Ta sáº½ tÃ¬m hiá»ƒu Terraform S3 Backend sáº½ bao gá»“m cÃ¡c thÃ nh pháº§n gÃ¬, táº¡o nÃ³ ra sao vÃ  á»©ng dá»¥ng nÃ³ vÃ o dá»± Ã¡n cá»§a ta tháº¿ nÃ o.

MÃ´ hÃ¬nh Ä‘Æ¡n giáº£n cá»§a S3 Standard Backend nhÆ° sau.

![image.png](https://images.viblo.asia/ebbe6c3a-b692-4421-a6cc-a3d36bae14a2.png)

Developing an S3 backend
------------------------

### Architecture

TrÆ°á»›c khi sá»­ dá»¥ng S3 backend thÃ¬ ta cáº§n pháº£i táº¡o nÃ³ trÆ°á»›c, cáº¥u trÃºc cá»§a má»™t S3 backend gá»“m nhá»¯ng thÃ nh pháº§n:

-   IAM
-   DynamoDB
-   S3 bucket - KMS

![image.png](https://images.viblo.asia/12f98b04-c5be-47ec-86b8-c0f35f2b8678.png)

Tá»«ng thÃ nh pháº§n trÃªn sáº½ Ä‘Æ°á»£c sá»­ dá»¥ng nhÆ° sau:

-   IAM Ä‘Æ°á»£c sá»­ dá»¥ng Ä‘á»ƒ terraform assume role, Ä‘á»ƒ terraform cÃ³ quyá»n ghi vÃ o dynamodb table vÃ  fetch/store state vÃ o bÃªn trong S3.
-   Dynamodb Ä‘Æ°á»£c terraform dÃ¹ng Ä‘á»ƒ ghi lock key cá»§a má»™t process vÃ o bÃªn trong nÃ³, vÃ¬ dynamodb cÃ³ tá»‘c Ä‘á»™ Ä‘á»c vÃ  ghi nhanh tá»›i má»©c milisecond nÃªn nÃ³ ráº¥t thÃ­ch há»£p Ä‘á»ƒ lock state cá»§a má»™t process.
-   S3 bucket dÃ¹ng Ä‘á»ƒ lÆ°u trá»¯ state khi terraform cháº¡y xong, KMS Ä‘Æ°á»£c S3 sá»­ dá»¥ng Ä‘á»ƒ mÃ£ hÃ³a dá»¯ liá»‡u state khi nÃ³ Ä‘Æ°á»£c lÆ°u vÃ o bÃªn trong S3.

### Developing

Giá» thÃ¬ ta sáº½ tiáº¿n hÃ nh táº¡o S3 backend, phÃ­a dÆ°á»›i cÃ¡c resource mÃ  ta sáº½ sá»­ dá»¥ng Ä‘á»ƒ táº¡o S3 backend.

![](https://images.viblo.asia/fe89e3b4-11f3-4d9d-83de-c65225a12b45.jpg)

Táº¡o má»™t má»™t folder vÃ  fileÂ `main.tf`Â +Â `variables.tf`Â +Â `versions.tf`Â vá»›i ná»™i dung.

```
provider "aws" {
  region = var.region
}

```

```
variable "region" {
  type = string
  default = "us-west-2"
}

variable "project" {
  description = "The project name to use for unique resource naming"
  default     = "terraform-series"
  type        = string
}

variable "principal_arns" {
  description = "A list of principal arns allowed to assume the IAM role"
  default     = null
  type        = list(string)
}

```

```
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.0"
    }
  }
}

```

Sau Ä‘Ã³ cháº¡y cÃ¢u lá»‡nhÂ `terraform init`. Oke, váº­y lÃ  bÆ°á»›c chuáº©n bá»‹ Ä‘Ã£ xong, tiáº¿p theo ta táº¡o fileÂ `dynamodb.tf`.

```
resource "aws_dynamodb_table" "dynamodb_table" {
  name         = "${var.project}-s3-backend"

  hash_key     = "LockID"
  billing_mode = "PAY_PER_REQUEST"

  attribute {
    name = "LockID"
    type = "S"
  }

  tags = local.tags
}

```

ÄÃ¢y lÃ  DynamoDB Table resource Ä‘á»ƒ chá»©a lock state cá»§a ta, ta Ä‘á»‹nh nghÄ©a table nÃ y sáº½ cÃ³ má»™t trÆ°á»ng lÃ Â LockID vá»›i type lÃ  StringÂ Ä‘Ã¢y lÃ  cáº¥u hÃ¬nh báº¯t buá»™c mÃ  terraform quy Ä‘á»‹nh cho table mÃ  dÃ¹ng Ä‘á»ƒ lÆ°u lock state.

Tiáº¿p theo ta táº¡o fileÂ `iam.tf`Â chá»©a cÃ¡c IAM resource.

```
data "aws_caller_identity" "current" {}

locals {
  principal_arns = var.principal_arns != null ? var.principal_arns : [data.aws_caller_identity.current.arn]
}

data "aws_iam_policy_document" "policy_doc" {
  statement {
    actions   = ["s3:ListBucket"]
    resources = [aws_s3_bucket.s3_bucket.arn]
  }

  statement {
    actions   = ["s3:GetObject", "s3:PutObject", "s3:DeleteObject"]
    resources = ["${aws_s3_bucket.s3_bucket.arn}/*"]
  }

  statement {
    actions   = ["dynamodb:GetItem", "dynamodb:PutItem", "dynamodb:DeleteItem"]
    resources = [aws_dynamodb_table.dynamodb_table.arn]
  }
}

```

Data sourceÂ `aws_caller_identity`Â dÃ¹ng Ä‘á»ƒ láº¥y thÃ´ng tin vá» aws account mÃ  ta Ä‘ang cháº¡y. Biáº¿n principal_arns sáº½ chá»©a táº¥t cáº£ Ä‘á»‘i tÆ°á»£ng mÃ  ta cho phÃ©p nÃ³ assume role vá»›i aws account cá»§a ta.

Tá»« biá»ƒu thá»©c so sÃ¡nhÂ `var.principal_arns != null ? var.principal_arns : [data.aws_caller_identity.current.arn]`Â á»Ÿ trÃªn => náº¿u ta khÃ´ng truyá»n biáº¿n nÃ y vÃ o khi cháº¡y terraform thÃ¬ nÃ³ sáº½ chá»‰ cho phÃ©p account mÃ  ta dÃ¹ng Ä‘á»ƒ cháº¡y terraform cÃ³ quyá»n assume role.

ResoruceÂ `aws_iam_policy_document`Â dÃ¹ng Ä‘á»ƒ Ä‘á»‹nh nghÄ©a cÃ¡c policy cá»§a ta, policy document á»Ÿ trÃªn sáº½ Ä‘á»‹nh nghÄ©a quyá»n cáº§n thiáº¿t Ä‘á»ƒ ta cÃ³ thá»ƒ thá»±c hiá»‡n hÃ nh Ä‘á»™ng lÃªn trÃªn DynamoDB, S3, KSM. Tiáº¿p theo ta sáº½ gáº¯n policy document nÃ y vÃ o policy vÃ  role.

```
...
resource "aws_iam_policy" "policy" {
  name   = "${title(var.project)}S3BackendPolicy"
  path   = "/"
  policy = data.aws_iam_policy_document.policy_doc.json
}

resource "aws_iam_role" "iam_role" {
  name = "${title(var.project)}S3BackendRole"

  assume_role_policy = <<-EOF
  {
    "Version": "2012-10-17",
    "Statement": [
      {
        "Action": "sts:AssumeRole",
        "Principal": {
        "AWS": ${jsonencode(local.principal_arns)}
      },
      "Effect": "Allow"
      }
    ]
  }
  EOF

  tags = local.tags
}

resource "aws_iam_role_policy_attachment" "policy_attach" {
  role       = aws_iam_role.iam_role.name
  policy_arn = aws_iam_policy.policy.arn
}

```

Sau Ä‘Ã³ ta táº¡o fileÂ `s3.tf`.

```
resource "aws_s3_bucket" "s3_bucket" {
  bucket        = "${var.project}-s3-backend"
  force_destroy = false

  tags = local.tags
}

resource "aws_s3_bucket_acl" "s3_bucket" {
  bucket = aws_s3_bucket.s3_bucket.id
  acl    = "private"
}

resource "aws_s3_bucket_versioning" "s3_bucket" {
  bucket = aws_s3_bucket.s3_bucket.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_kms_key" "kms_key" {
  tags = local.tags
}

resource "aws_s3_bucket_server_side_encryption_configuration" "s3_bucket" {
  bucket = aws_s3_bucket.s3_bucket.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm     = "aws:kms"
      kms_master_key_id = aws_kms_key.kms_key.arn
    }
  }
}

```

ResourceÂ `aws_s3_bucket`Â ta dÃ¹ng Ä‘á»ƒ Ä‘á»‹nh nghÄ©a s3 bucket,Â `aws_s3_bucket_acl`Â ta dÃ¹ng Ä‘á»ƒ Ä‘á»‹nh nghÄ©a access control list cá»§a s3, ta nÃªn Ä‘á»ƒ lÃ  private.

Tiáº¿p theo vÃ  quan trá»ng lÃ  Ä‘á»ƒ S3 cÃ³ thá»ƒ dÃ¹ng Ä‘á»ƒ lÆ°u trá»¯ state, ta pháº£i báº­t versioning cho nÃ³, ta lÃ m báº±ng resourceÂ `aws_s3_bucket_versioning`. Cuá»‘i cÃ¹ng lÃ  ta báº­t SSE (Server Side Encryption) cho bucket cá»§a ta báº±ng resourceÂ `aws_s3_bucket_server_side_encryption_configuration`.

Oke, váº­y lÃ  ta Ä‘Ã£ chuáº©n bá»‹ Ä‘á»§ cÃ¡c resource cho S3 backend, tiáº¿p theo ta cáº­p nháº­t láº¡i fileÂ `main.tf`Â Ä‘á»ƒ nÃ³ output ra giÃ¡ trá»‹ cá»§a S3 backend mÃ  ta sáº½ cáº§n, Ä‘á»ƒ sá»­ dá»¥ng cho cÃ¡c terraform project khÃ¡c.

```
...
locals {
  tags = {
    project = var.project
  }
}

data "aws_region" "current" {}

resource "aws_resourcegroups_group" "resourcegroups_group" {
  name = "${var.project}-s3-backend"

  resource_query {
    query = <<-JSON
      {
        "ResourceTypeFilters": [
          "AWS::AllSupported"
        ],
        "TagFilters": [
          {
            "Key": "project",
            "Values": ["${var.project}"]
          }
        ]
      }
    JSON
  }
}

output "config" {
  value = {
    bucket         = aws_s3_bucket.s3_bucket.bucket
    region         = data.aws_region.current.name
    role_arn       = aws_iam_role.iam_role.arn
    dynamodb_table = aws_dynamodb_table.dynamodb_table.name
  }
}

```

Báº¡n sáº½ Ä‘á»ƒ Ã½ tháº¥y cÃ³ resource tÃªn lÃ Â `aws_resourcegroups_group`, tháº±ng nÃ y chá»§ yáº¿u Ä‘Æ°á»£c dÃ¹ng Ä‘á»ƒ group resource láº¡i cho ta dá»… quáº£n lÃ½ thÃ´i, lÃ¡t mÃ¬nh sáº½ show cho cÃ¡c báº¡n xem.

Giá» báº¡n cháº¡y cÃ¢u lá»‡nhÂ `terraform plan`Â Ä‘á»ƒ táº¡o S3 backend, sau khi cháº¡y xong ta sáº½ tháº¥y output nhÆ° dÆ°á»›i, Ä‘Ã¢y lÃ  cÃ¡c giÃ¡ trá»‹ ta sáº½ cáº§n.

```
config = {
  "bucket" = "terraform-series-s3-backend"
  "dynamodb_table" = "terraform-series-s3-backend"
  "region" = "us-west-2"
  "role_arn" = "arn:aws:iam::<ACCOUNT_ID>:role/HpiS3BackendRole"
}

```

Äá»ƒ kiá»ƒm tra cÃ¡c resource cá»§a S3 backend, ta truy cáº­p AWS ConsoleÂ <https://console.aws.amazon.com/resource-groups/home>, báº¡n sáº½ tháº¥y resource group cá»§a ta.

![image.png](https://images.viblo.asia/db675355-5957-41e1-ad82-3fa0af5a1033.png)

Báº¥m vÃ o nÃ³ báº¡n sáº½ tháº¥y chi tiáº¿t cá»§a tá»«ng resource cá»§a S3 backend. Oke, tiáº¿p theo ta sáº½ tiáº¿n hÃ nh sá»­ dá»¥ng S3 backend nÃ y vÃ o trong project ğŸ˜.

### Using S3 backend

Äá»ƒ sá»­ dá»¥ng S3 backend cho má»™t project, ta cáº¥u cáº§n cáº¥u hÃ¬nh nhÆ° sau.

```
terraform {
  backend "s3" {
    bucket         = <bucket-name>
    key            = <path>
    region         = <region>
    encrypt        = true
    role_arn       = <arn-role>
    dynamodb_table = <dynamodb-table-name>
  }
}

```

Ta sáº½ khai bÃ¡o má»™t block tÃªn lÃ  terraform vá»›i backend lÃ  s3 vá»›i cÃ¡c giÃ¡ trá»‹ sau:

-   bucket: s3 bucket name.
-   key: path ta lÆ°u state trong bucket.
-   role_arn: IAM role mÃ  cÃ³ quyá»n cáº§n thiáº¿t.
-   dynamodb_table: table dÃ¹ng Ä‘á»ƒ save lock state.

Giá» ta sáº½ lÃ m vÃ­ dá»¥ táº¡o má»™t EC2 mÃ  sá»­ dá»¥ng S3 backend. Táº¡o má»™t folder vÃ  fileÂ `main.tf`.

```
terraform {
  backend "s3" {
    bucket         = "terraform-series-s3-backend"
    key            = "test-project"
    region         = "us-west-2"
    encrypt        = true
    role_arn       = "arn:aws:iam::<ACCOUNT_ID>:role/HpiS3BackendRole"
    dynamodb_table = "terraform-series-s3-backend"
  }
}

provider "aws" {
  region = "us-west-2"
}

data "aws_ami" "ami" {
  most_recent = true

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }

  owners = ["099720109477"]
}

resource "aws_instance" "server" {
  ami           = data.aws_ami.ami.id
  instance_type = "t3.micro"

  lifecycle {
    create_before_destroy = true
  }

  tags = {
    Name = "Server"
  }
}

output "public_ip" {
  value = aws_instance.server.public_ip
}

```

Cháº¡yÂ `terraform init`Â sau Ä‘Ã³ ta cháº¡yÂ `terraform plan`, sau khi nÃ³ cháº¡y xong báº¡n sáº½ tháº¥yÂ `terraform.tfstate`Â sáº½ khÃ´ng cÃ²n náº±m á»Ÿ local ná»¯a. MÃ  ta sáº½ cáº§n lÃªn trÃªn S3 bucket Ä‘á»ƒ xem state file cá»§a ta.

Truy cáº­p AWS S3 ConsoleÂ <https://s3.console.aws.amazon.com/s3/buckets>.

![image.png](https://images.viblo.asia/42917c37-36f1-470b-9a0a-75661acc8c03.png)

Báº¥m vÃ o terraform-series-s3-backend ta sáº½ tháº¥y state file cá»§a ta.

![image.png](https://images.viblo.asia/01cb095a-f4ab-4682-ac7d-fbb182f477dc.png)

Oke, s3 backend cá»§a ta Ä‘Ã£ Ä‘Æ°á»£c implement thÃ nh cÃ´ng ğŸ˜. Github repo cá»§a toÃ n bá»™ seriesÂ <https://github.com/hoalongnatsu/terraform-series.git>.

Káº¿t luáº­n
--------

Váº­y lÃ  ta Ä‘Ã£ tÃ¬m hiá»ƒu xong vá» S3 backend, cÃ¡ch táº¡o ra nÃ³ vÃ  cÃ¡ch sá»­ dá»¥ng nÃ³. Khi ta lÃ m viá»‡c vá»›i team thÃ¬ ta nÃªn sá»­ dá»¥ng S3 backend cho project cá»§a ta, vá»«a centralized Ä‘Æ°á»£c state file, vá»«a giáº£i quyáº¿t Ä‘Æ°á»£c váº¥n Ä‘á»ƒ conflict khi nhiá»u ngÆ°á»i cháº¡y terraform project cÃ¹ng má»™t lÃºc